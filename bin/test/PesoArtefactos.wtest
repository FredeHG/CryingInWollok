import artefactos.*
import efectosGlobales.*
import refuerzos.*
import personaje.*
import spells.*
import feriaHechiceria.*
import Comerciantes.*

describe "Peso de los artefactos" {

	const mascaraOscura = new MascaraOscura(1, 3, fechaActual.fecha())
	const mascaraClara = new MascaraOscura(0, 2, fechaActual.fecha())
	const cotaDeMalla = new CotaDeMallas(0)
	const armaduraConCotaDeMalla = new Armadura(cotaDeMalla, 2, 10, fechaActual.fecha())
	const duenioGenerico = new Personaje([], [], "", 0)

	test "Saber el peso de la espada vieja" {
		const espadaVieja = new Arma(7, new Date(7, 5, 2006))
		assert.equals(6, espadaVieja.pesoTotal(duenioGenerico))
	}

	test "Saber el peso de la espada nueva" {
		const espadaNueva = new Arma(4, fechaActual.fecha())
		assert.equals(4, espadaNueva.pesoTotal(duenioGenerico))
	}

	test "Saber el peso de la espada comun" {
		const espadaComun = new Arma(5, fechaActual.fecha().minusDays(50))
		assert.equals(4.5, espadaComun.pesoTotal(duenioGenerico))
	}

	test "Saber el peso de la máscara oscura" {
		assert.equals(4, mascaraOscura.pesoTotal(duenioGenerico))
	}

	test "Saber el peso del collar divino" {
		const collarDivino = new CollarDivino(5, 0, fechaActual.fecha())
		assert.equals(2.5, collarDivino.pesoTotal(duenioGenerico))
	}

	test "Hacer que ocurra un eclipse y Saber el peso de la máscara oscura" {
		fuerzaOscura.eclipse()
		assert.equals(5, mascaraOscura.pesoTotal(duenioGenerico))
	}

	test "Saber el peso de la máscara clara" {
		assert.equals(2, mascaraClara.pesoTotal(duenioGenerico))
	}

	test "Saber cuál es el peso total de la armadura con cota de malla" {
		assert.equals(11, armaduraConCotaDeMalla.pesoTotal(duenioGenerico))
	}

	test "Saber cuál es el peso total de la armadura con hechizo par" {
		const hechizoComercial = new HechizoComercial()
		const armaduraHechizoPar = new Armadura(new Hechizo(hechizoComercial), 2, 12, fechaActual.fecha())
		assert.equals(14, armaduraHechizoPar.pesoTotal(duenioGenerico))
	}

	test "Saber cuál es el peso total de la armadura con hechizo impar" {
		const espectroMalefico = new Logo("espectro malefico", 1)
		const armaduraHechizoImpar = new Armadura(new Hechizo(espectroMalefico), 2, 12, fechaActual.fecha())
		assert.equals(13, armaduraHechizoImpar.pesoTotal(duenioGenerico))
	}

	test "Saber cuál es el peso total de la armadura sin ningún tipo de refuerzo" {
		const otraArmadura = new Armadura(ninguno, 2, 12, fechaActual.fecha())
		assert.equals(12, otraArmadura.pesoTotal(duenioGenerico))
	}

	test "Merlín le compra a Mock Hnos. la máscara clara y la oscura (sin que haya eclipse). Saber cuál es el peso total que carga Merlin." {
		const mockHnos = new Comerciante(tipoIndependiente)
		const merlin = new Personaje([], [], "", 10)
		merlin.comprarArtefacto(mascaraClara, mockHnos)
		merlin.comprarArtefacto(mascaraOscura, mockHnos)
		assert.equals(6, merlin.cargaActual())
	}

	test "Ursula carga con 14 kilos iniciales. Decide desprenderse de la armadura con cota de malla.
			 Saber cuál es el peso total con el que carga Ursula ahora." {
		const ursula = new Personaje([ mascaraOscura, armaduraConCotaDeMalla ], [], "", 200)
		ursula.removerArtefacto(armaduraConCotaDeMalla)
		assert.equals(4, ursula.cargaActual())
	}

}

